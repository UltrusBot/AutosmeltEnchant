plugins {
    alias libs.plugins.fabric.loom
    alias libs.plugins.loom.quiltflower
    alias libs.plugins.publishing
    id 'maven-publish'
}
version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name 'Quilt'
        url 'https://maven.quiltmc.org/repository/release'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft(libs.minecraft)
    mappings(variantOf(libs.quilt.mappings) { classifier("intermediary-v2") })
    modImplementation(libs.fabric.loader)
    modImplementation(libs.fabric.api)
}
publishMods {
    displayName = "Smelting Touch v$project.version"
    file = remapJar.archiveFile
    changelog = file("$rootDir/CHANGELOG.md").text
    version = project.version
    type = STABLE
    modLoaders.addAll("fabric", "quilt")
    curseforge {
        projectId = "470405"
        minecraftVersions.add("1.20.2")
        accessToken = providers.environmentVariable("CF_API_KEY")
    }
    modrinth {
        projectId = "otiSEfKe"
        minecraftVersions.add("1.20.2")
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
    }
    github {
        repository = "UltrusBot/AutosmeltEnchant"
        commitish = "1.20.2"
        accessToken = providers.environmentVariable("GH_TOKEN")
        tagName = "v$project.version"
    }

    dryRun = false
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "LICENSE_${archives_base_name}" }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}



java {
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}